import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.15.0"
    id "com.xebialabs.xldp" version "1.0.5"
    id 'jacoco'
    id "org.sonarqube" version "2.8"
    id 'maven-publish'
    id 'signing'
}

group = "com.rabobank.argos"
archivesBaseName = "xld-argos-plugin"
version = "0.1.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8
def apiVersion = '2019.3.4'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
	mavenLocal()
    mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url 'https://dist.xebialabs.com/public/maven2'
    }
}

dependencies {
    compile "com.xebialabs.deployit:udm-plugin-api:$apiVersion"
    compile "com.xebialabs.deployit:engine-api:$apiVersion"
    compile "com.rabobank.argos:argos4j:0.1.0"
    compile "org.projectlombok:lombok:1.18.12"
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    
    testCompile "org.junit.jupiter:junit-jupiter:5.6.2"
    testCompile "org.hamcrest:hamcrest-library:2.2"
    testCompile "org.junit.jupiter:junit-jupiter-params:5.6.2"    
    testCompile "org.mockito:mockito-junit-jupiter:3.5.5"
    
    distBundle "com.rabobank.argos:argos4j:0.1.0"
    
}

processResources.configure {
  filter ReplaceTokens, tokens: [
    'project.version': version.toString(),
    'project.name': rootProject.name
  ]
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = true
    }
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, xlPlugin
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    
    sign configurations.archives
}

license {
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    header rootProject.file('src/main/license/license_header.txt')
    strictCheck true
}

uploadArchives {
  def ossrhLoginUser = findProperty("ossrhLoginUser")
  def ossrhLoginPassword = findProperty("ossrhLoginPassword")

  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhLoginUser, password: ossrhLoginPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhLoginUser, password: ossrhLoginPassword)
      }

      pom.project {
        name 'XLDeploy Argos plugin'
        packaging 'xldp'
        
        description 'A plugin used in XLDeploy to communicate with an Argos Service.'
        url 'https://github.com/argosnotary/argos/tree/master'

        scm {
          connection 'scm:git:https://github.com/argosnotary/xld-argos-plugin.git'
          developerConnection 'scm:git:https://github.com/argosnotary/xld-argos-plugin.git'
          url 'https://github.com/argosnotary/argos/tree/master'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'borstg'
            name 'Gerard Borst'
            email 'gerard.borst@rabobank.nl'
          }
        }
      }
    }
  }
}

